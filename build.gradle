buildscript {
  dependencies {
    classpath 'net.sf.proguard:proguard-gradle:6.0.3'
  }
}

plugins {
    id 'com.typelead.eta' version '0.7.9'
    id 'com.github.johnrengelman.shadow' version '2.0.3'
}

allprojects {
  repositories {
    mavenCentral()
  }
}

compileTestEta.enabled = false

eta {
  version = '0.8.6b3'
  useSystemEta = true
  etlasVersion = '1.6.0.0'
  useSystemEtlas = true
}

compileEta {
  options {
    args = ['-Wall', '-fno-warn-safe', '-fno-warn-unsafe', '-fno-warn-implicit-prelude',
            '-fno-warn-missing-import-lists', '-fno-warn-missing-local-sigs', 
            '-fno-warn-monomorphism-restriction', '-fno-warn-name-shadowing']
    extensions {
      OverloadedStrings
      DataKinds
      TypeOperators
      TypeFamilies
      FlexibleContexts
      MultiParamTypeClasses
    }
  }
}

project.version = '0.1.0'

dependencies {
  compile project(':java')
  compile ('org.bouncycastle:bcprov-jdk15on:1.59')

  compile eta('base:[4.5,5[')
  compile eta('bytestring:[0.10,0.11[')
  compile eta('containers:[0.5,0.6[')
  compile eta('contravariant:[1.5,1.6[')
  compile eta('cryptonite:[0.23,1.0[')
  compile eta('dhall:[1.19.1,1.20[')
  compile eta('dotgen:[0.4.2,0.5[')
  compile eta('eta-java-interop:[0.1.5.0,0.1.6[')
  compile eta('megaparsec:[6.1.1,7.1[')
  compile eta('memory:[0.14,0.15[')
  compile eta('lens-family-core:[1.0.0,1.3[')
  compile eta('prettyprinter:[1.2.0.1,1.3[')
  compile eta('scientific:[0.3.0.0,0.4[')
  compile eta('serialise:[0.2.0.0,0.3[')
  compile eta('text:[1.2,1.3[')
  compile eta('transformers:[0.2.0.0,0.6[')
}

shadowJar {
  zip64 = true
}

import proguard.gradle.ProGuardTask

task proJar(type: ProGuardTask) {
  configuration defaultEtaProguardFile
  configuration 'proguard.txt'

  injars shadowJar.outputs
  outjars "build/dhall-eta-${project.version}-uber.jar"
}
